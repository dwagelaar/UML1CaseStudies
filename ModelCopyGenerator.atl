-- $Id$
query ModelCopyGenerator = INMODEL!Class.allInstances()->iterate(e; acc : String = 
	'-- Generated by: $Id$\n' +
	'module ModelCopy;\n\n' +
	'create OUT : OUTMODEL from IN : INMODEL;\n\n'|
	acc + e.toString()).
	writeTo('my_documents/PhD/Brainstorm/UML1CaseStudies/ModelCopy.atl');

-- adapt the above 'writeTo' expression to reflect your file path

helper context INMODEL!Class def : inclusionCondition() : Boolean =
    self.name = 'Abstraction' or
    self.name = 'Actor' or
    self.name = 'Argument' or
    self.name = 'AssociationEnd' or
    self.name = 'Association' or
    self.name = 'Attribute' or
    self.name = 'CallAction' or
    self.name = 'Class' or
    self.name = 'CollaborationInstanceSet' or
    self.name = 'Collaboration' or
    self.name = 'DataType' or
    self.name = 'Dependency' or
    self.name = 'Exception' or
    self.name = 'Expression' or
    self.name = 'Generalization' or
    self.name = 'Include' or
    self.name = 'InteractionInstanceSet' or
    self.name = 'Interface' or
    self.name = 'LinkEnd' or
    self.name = 'Link' or
    self.name = 'Message' or
    self.name = 'Method' or
    self.name = 'Model' or
    self.name = 'Multiplicity' or
    self.name = 'MultiplicityRange' or
    self.name = 'Object' or
    self.name = 'Operation' or
    self.name = 'Package' or
    self.name = 'Parameter' or
    self.name = 'ProcedureExpression' or
    self.name = 'ReturnAction' or
    self.name = 'Stereotype' or
    self.name = 'Stimulus' or
    self.name = 'TagDefinition' or
    self.name = 'TaggedValue' or
    self.name = 'UseCase';

-- adapt the above inclusion condition for your specific model

helper context INMODEL!Class def : toString() : String =
	if not self.isAbstract and self.inclusionCondition() then
		'rule ' + self.name + ' {\n' +
		'    from s : INMODEL!' + self.name + self.inputConstraint() + '\n' +
		'    to t : OUTMODEL!' + self.name + ' mapsTo s (' +
		self.contentsToString() + ')\n' +
		'}\n\n'
	else
		''
	endif;

helper context INMODEL!GeneralizableElement def : inputConstraint() : String =
	if self.hasConcreteSubclasses() then
		' (s.oclIsTypeOf(INMODEL!' + self.name + '))'
	else
		''
	endif;

helper context INMODEL!GeneralizableElement def : hasConcreteSubclasses() : Boolean =
	not INMODEL!GeneralizableElement.allInstances()->select(e|
		e.supertypes->includes(self) and not e.isAbstract)->isEmpty();

helper context INMODEL!GeneralizableElement def : includesMember(member : INMODEL!ModelElement) : Boolean =
	self.contents->includes(member) or
	not self.supertypes->select(s|s.includesMember(member))->isEmpty();

helper context INMODEL!GeneralizableElement def : contentsToString() : String =
	self.referencesToString(self.attributesToString(''));

helper context INMODEL!GeneralizableElement def : attributesToString(head : String) : String =
	self.featuresToString(head, INMODEL!Attribute.allInstances());

helper context INMODEL!GeneralizableElement def : referencesToString(head : String) : String =
	self.featuresToString(head, INMODEL!Reference.allInstances()->
		select(r|r.referencedEnd.aggregation=#none));

helper context INMODEL!GeneralizableElement def : featuresToString(head : String, feature : Sequence(INMODEL!StructuralFeature)) : String =
	feature->select(f|self.includesMember(f))->iterate(e; acc : String = head |
		if acc.size() = 0 then
			'\n' + e.toString()
		else
			acc + ',\n' + e.toString()
		endif);

helper context INMODEL!ModelElement def : toAssignString() : String =
	if self.name.isKeyword() then
		'        "' + self.name + '" <- s."' + self.name + '"'
	else
		'        ' + self.name + ' <- s.' + self.name
	endif;

helper context INMODEL!Attribute def : toString() : String =
	self.toAssignString();

helper context INMODEL!Reference def : toString() : String =
	self.toAssignString();

helper context String def : isKeyword() : Boolean =
	Set{'module', 'create', 'from', 'rule', 'to', 'using', 'helper', 'context',
		'def', 'and', 'or', 'not', 'if', 'then', 'else', 'endif', 'query',
		'library', 'mapsTo', 'String', 'Boolean', 'Integer', 'Real', 'Bag',
		'Set', 'OrderedSet', 'Sequence'}->includes(self);
